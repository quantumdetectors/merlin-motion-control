#AUTO

safe=0
unsafe=1

OB1, unsafe

AG0
YA 16
SH
WT50

merspeed=20000
merspeec=20000
merin=0
lmerin=0
merstat=0
meroff=0
decel=2000000
req_pos=0
stdbypos=0

IF (_LRX=0)
	MG "Moving off limit"
	JS#HOME2
ENDIF

JS#HOME
JS#PNEU

AC1000000
DC=decel

JP#LOOP

#LOOP
'MG "Loop"
IF (merin <> lmerin)
	lmerin=merin
	'MG "Mer changed"
	'MG merin
	IF (merin=1)
		IF ((@IN[1]=0)|(@IN[2]=0))
			OB1, unsafe
			merin=0
		ELSE
			JS #MOIN
		ENDIF
	ELSE
		IF (merin=2)
			IF ((@IN[1]=0)|(@IN[2]=0))
				OB1, unsafe
				merin=0
			ELSE
				JS #STP
			ENDIF
		ELSE
			IF (merin=3)
				IF ((@IN[1]=0)|(@IN[2]=0))
					OB1, unsafe
					merin=0
				ELSE
					JS #STDBY
				ENDIF
			ELSE
				JS #HOME
			ENDIF
		ENDIF
	ENDIF
	JS#SETOP
ENDIF
WT1000
JP#LOOP
EN


#STP
'MG "Stop"
#Q
IF (merin <> 2)
    JS #LOOP
ENDIF
JP#Q, _LRX = 0, _LFX = 0
MO
merstat=3
'MG "Stopped"
EN

#HOME
'MG "move out"
merstat=2
SH
DCX=decel
JGX=-1*merspeec
BGX
#L
IF (merin <> 0)
	STX
	AMX
	JS #LOOP
ENDIF
JP#L, _LRX = 1
STX
AMX
#HOME2
DCX=67107840
JGX=500
BGX
#M
IF (merin <> 0)
	DCX=decel
	STX
	AMX
	JS #LOOP
ENDIF
JP#M, _LRX = 0
STX
AMX
DP0
MO
merstat=0
MG "Homed"
EN

#MOIN
'MG "move in"
merstat=2
SH
DCX=decel
JGX=merspeed
PA req_pos
BGX
'MF req_pos
#A
IF (merin <> 1)
	STX
	AMX
	JS #LOOP
ENDIF
JP#A, _BGX = 1
AMX
merstat=1
'MG "Moved"
MO
EN

#STDBY
'MG "Going to standby"
merstat=2
SH
DCX=decel
JGX=merspeed
PA stdbypos
BGX
#AA
IF (merin <> 3)
	STX
	AMX
	JS #LOOP
ENDIF
JP#AA, _BGX = 1
AMX
merstat=4
'MG "In Standby"
MO
EN

#ININT
'MG "Int"
WT10
JS#SETOP

IF ((@IN[2]=0)&(@IN[1]=0))
	trigger=3
ELSE
	IF ((@IN[2]=1)&(@IN[1]=0))
		trigger=1
	ELSE
		IF ((@IN[2]=0)&(@IN[1]=1))
			trigger=2
		ELSE
			IF ((@IN[2]=1)&(@IN[1]=1))
				trigger=0
			ENDIF
		ENDIF
	ENDIF
ENDIF
ZS
IF ((merin=0)&(merstat=2))
	JS#HOME
	lmerin=0
	JS#SETOP
ENDIF
II1,2,,trigger
JP#LOOP
EN
RI0

#PNEU
JS#SETOP
IF ((@IN[2]=0)&(@IN[1]=0))
	trigger=3
ELSE
	IF ((@IN[2]=1)&(@IN[1]=0))
		trigger=1
	ELSE
		IF ((@IN[2]=0)&(@IN[1]=1))
			trigger=2
		ELSE
			IF ((@IN[2]=1)&(@IN[1]=1))
				trigger=0
			ENDIF
		ENDIF
	ENDIF
ENDIF
II1,2,,trigger
EN

#SETOP
IF (((@IN[1]=0)&(_RPA>10))|((@IN[2]=0) & (_RPA>10)))
	OB1, unsafe
	IF (merin=1)
		STX
		AMX
		merin=0
	ELSE
		IF (merin=2)
			STX
			AMX
			merin=0
		ELSE
			IF (merin=3)
				STX
				AMX
				merin=0
			ENDIF
		ENDIF
	ENDIF
ELSE
	IF (merin=0) & (merstat=2)
		WT2000
	ENDIF
	OB1, safe
ENDIF
EN
